#!/usr/bin/awk -f
 
# Format the "affected_uncommitted.audit" record matching the specified file
# Usage: astat "<file>"
 
BEGIN {
   FS=","
   file = "\"" ARGV[1] "\""
   ARGV[1] = "affected_uncommitted.audit"
   w_auto_offset = 30*86400
   printf "REFERENCE: %s/%s\n",ENVIRON["PWD"],ARGV[1]
   print "FILE: " file
}
{
   if ($2 == "-") committed = "UNCOMMITTED"
   else if ($2 == "C") committed = "COMMITTED"
   else if ($2 == "c") committed = "LATENT"
   else if ($2 == "X") committed = "EXPIRED"
   else committed = "FUBAR"
   ref_date = $3
   st_atime = $4
   st_mtime = $5
   st_ctime = $6
   st_birthtime = $7
   w_ctime = $8
   w_retention_date = $9
   autocommit_target = st_ctime + w_auto_offset
   afile = $16
 
   found = 0
 
   if (file == afile) {
      found = 1
      printf "           ref_date = %lu - ",ref_date
        system("date -r" ref_date)
      printf "       st_birthtime = %lu - ",st_birthtime
        system("date -r" st_birthtime)
      printf "           st_atime = %lu - ",st_atime
        system("date -r" st_atime)
      printf "           st_mtime = %lu - ",st_mtime
        system("date -r" st_mtime)
      printf "           st_ctime = %lu - ",st_ctime
        system("date -r" st_ctime)
      printf "            w_ctime = %lu - ",w_ctime
        system("date -r" w_ctime)
      printf "        ctime_delta = %d (%6.3f days)\n",w_ctime-st_ctime,(w_ctime-st_ctime)/86400
      print  "          committed = " committed
      if (w_retention_date) {
         printf "   w_retention_date = %lu - ",w_retention_date
                system("date -r" w_retention_date)
      } else {
         printf "   w_retention_date = 0\n"
      }
      printf "  autocommit_target = %lu - ",autocommit_target
        system("date -r" autocommit_target)
      time_to_target =  autocommit_target-ref_date
      printf "     time_to_target = %d (%6.3f days) (negative is LATE)\n",time_to_target,time_to_target/86400
      exit 0
   }
}
END {
   if (!found) print "File not found."
}
 
# Column indexes for -audit ...
#  1.  lock_domain_type - SmartLock domain type;
#    'E' - Enterprise
#    'C' - Compliance
#    '-' - Neither
#  2.  lock_status - SmartLock lock status;
#    '-' - Not locked
#    'C' - Committed (READONLY, NON-DELETABLE)
#    'c' - Latent Commit (READONLY, ?POSSIBLY-DELETABLE?)
#    'X' - eXpired (READONLY, DELETABLE)
#  3.  w_ref_date - Reference time of worm status enquiry
#  4.  sb->st_atime
#  5.  sb->st_mtime
#  6.  sb->st_ctime
#  7.  sb->st_birthtime
#  8.  w_ctime
#  9.  w_retention_date
#  10.  eff_auto_date - Ephemeral AutoCommit date
#  11.  eff_exp_type - Basis of effective expiration date;
#     '<' - past (persisted,expired; committed with eff_exp_date in the past)
#     '>' - future (persisted, committed)
#     '=' - forced (persisted, uncommitted w/ w_retention_date from WORM state)
#     '!' - future (override)
#     '*' - future (ephemeral, based on future autocommit)
#     '+' - TBD (expiration not ascertainable)
#     '?' - ERROR: unexpected case w/ persisted expiration!
#     '#' - ERROR: unexpected fall-through
#  12.  eff_exp_date - Effective expiration date
#  13.  sb->st_uid
#  14.  sb->st_size
#  15.  sb->st_blocks
#  16.  "<ifspath>"
